plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'com.santander.springcepapi'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Jakart
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'

    // Webflux
    implementation 'org.springframework:spring-webflux'

    // Dynamodb
    implementation 'software.amazon.awssdk:dynamodb:2.31.30'
    implementation 'software.amazon.awssdk:dynamodb-enhanced:2.31.30'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.6.3'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.17.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    //Geral
    implementation 'org.glassfish.jersey.core:jersey-common:3.1.10'
    implementation 'io.projectreactor.netty:reactor-netty:1.3.0-M2'

}

test {
    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'dev'

    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.13"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        csv.required = false
        html.required = true
        html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80  // 80% de cobertura m√≠nima
            }
            excludes = []
        }

        rule {
            element = 'METHOD'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
}

def excludedFiles = []

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: excludedFiles)
        }))
    }
}

jacocoTestCoverageVerification {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: excludedFiles)
        }))
    }
}

tasks.register('checkCoverage') {
    dependsOn test
    dependsOn jacocoTestReport
    dependsOn jacocoTestCoverageVerification
}

bootJar {
    version = ''
    archiveClassifier = ''
}
